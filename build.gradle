plugins() {
    id('java')
    id('com.gradleup.shadow').version('9.0.0-beta4')
}

group = 'cn.flowerinsnow.grandpacleaner'
version = '1.0.0'

repositories() {
    mavenCentral()
    maven() {
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
}

configurations() {
    shade
    implementation.extendsFrom(shade)
}

dependencies() {
    compileOnly('dev.folia:folia-api:1.21.4-R0.1-SNAPSHOT')
    compileOnly('org.jetbrains:annotations:26.0.1')

    shade('cc.carm.lib:mineconfiguration-bukkit:2.9.3')
    shade('cc.carm.lib:easyplugin-gui:1.5.12')

    testImplementation(platform('org.junit:junit-bom:5.11.3'))
    testImplementation('org.junit.jupiter:junit-jupiter')
}

java() {
    toolchain() {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

shadowJar() {
    from('LICENSE')
    from('NOTICE')

    configurations = [project.configurations.shade]

    archiveClassifier = ''
}

processResources() {
    LinkedHashMap<String, ?> props = [
            'version': project.version
    ]
    props.forEach(inputs::property)
    filesMatching('plugin.yml') {
        expand(props)
    }
}

test() {
    useJUnitPlatform()
}
